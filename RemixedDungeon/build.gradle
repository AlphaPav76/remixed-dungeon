apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'com.google.firebase.crashlytics'


ext.getApiField = { name ->
    def stringsFile = "RemixedDungeon/src/main/res/values/strings_api_signature.xml"
    return new XmlParser().parse(stringsFile).string.find { it.@name.equals name }.text()
}

repositories {
    google()
    maven { url "https://artifactory.appodeal.com/appodeal" }
}

def jks_keyAlias    = System.getenv('jks_keyAlias') ? System.getenv('jks_keyAlias') : 'testKey'
def jks_keyPassword = System.getenv('jks_keyPassword') ? System.getenv('jks_keyPassword') : '123456'
def jks_storePassword = System.getenv('jks_storePassword') ? System.getenv('jks_storePassword') : '123456'
def jks_storePath = System.getenv('jks_storePath')  ? System.getenv('jks_storePath') : 'testKey/test.jks'

android {

    signingConfigs {
        release {
            keyAlias jks_keyAlias
            keyPassword jks_keyPassword
            storeFile file(jks_storePath)
            storePassword jks_storePassword
        }
    }

    defaultConfig {
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        manifestPlaceholders = ["admob_app_id":getApiField("admob_app_id"),
                                "gow_app_id":getApiField("gow_app_id")]

        applicationId "com.nyrds.pixeldungeon.ml"
        versionCode 861
        versionName "29.4.beta.2"
    }

    aaptOptions {
        noCompress "mp3","ogg"
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            minifyEnabled false
        }
        debug {
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility '1.8'
        targetCompatibility '1.8'
    }

    dexOptions {
        javaMaxHeapSize "4g"
        additionalParameters = ["--set-max-idx-number=45000"]
    }

    lintOptions {
        abortOnError false
    }

    flavorDimensions "minApi"
    productFlavors {
        googlePlay {
            buildToolsVersion = '29.0.2'
            targetSdkVersion 29
            minSdkVersion 16
            compileSdkVersion = 29

            versionCode defaultConfig.versionCode + 10000
            // Enabling multidex support.
            multiDexEnabled true
            dimension "minApi"
        }

    }


}


dependencies {
    implementation project(':annotation')

    annotationProcessor project(':processor')

    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'org.luaj:luaj-jse:3.0.1'
    implementation 'info.guardianproject.netcipher:netcipher:1.2'

    implementation 'com.google.android.ads.consent:consent-library:1.0.8'

    implementation 'com.google.android.gms:play-services-base:17.2.1'
    implementation 'com.google.android.gms:play-services-auth:17.0.0'
    implementation "com.google.android.gms:play-services-games:19.0.0"

    implementation 'com.android.support:multidex:1.0.3'

    implementation 'io.humanteq.hqm:hqm-core:2.0.4'

    implementation 'com.android.billingclient:billing:2.2.0'
    implementation 'commons-io:commons-io:2.5'

    implementation 'com.google.firebase:firebase-analytics:17.3.0'
    implementation 'com.google.firebase:firebase-perf:19.0.5'
    implementation 'com.google.firebase:firebase-ads:19.0.1'
    implementation 'com.google.firebase:firebase-crashlytics:17.0.0-beta03'


    implementation 'com.appodeal.ads.sdk:core:2.7.0'
    implementation 'com.appodeal.ads.sdk.networks:adcolony:2.6.0.2'
    implementation 'com.appodeal.ads.sdk.networks:applovin:2.6.0.2'
    implementation 'com.appodeal.ads.sdk.networks:appodealx:2.6.0.1'
    implementation 'com.appodeal.ads.sdk.networks:chartboost:2.7.0.2'
    implementation 'com.appodeal.ads.sdk.networks:inmobi:2.7.0.2'
    implementation 'com.appodeal.ads.sdk.networks:ironsource:2.7.0.3'
    implementation 'com.appodeal.ads.sdk.networks:mraid:2.6.0.1'
    implementation 'com.appodeal.ads.sdk.networks:smaato:2.6.0.2'
    implementation 'com.appodeal.ads.sdk.networks:tapjoy:2.7.0.3'
    implementation 'com.appodeal.ads.sdk.networks:unity_ads:2.7.0.3'
    implementation 'com.appodeal.ads.sdk.networks:vast:2.6.0.1'
    implementation 'com.appodeal.ads.sdk.networks:vungle:2.6.0.1'

    implementation 'org.jetbrains:annotations:15.0'

    compileOnly 'org.projectlombok:lombok:1.18.10'
    annotationProcessor 'org.projectlombok:lombok:1.18.10'

    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test:core:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
}


apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.getkeepsafe.dexcount'
