apply plugin: 'com.android.application'


def getApiField(String name) {
    def stringsFile = android.sourceSets.main.res.sourceFiles.find { it.name.equals 'strings_api_signature.xml' }
    return new XmlParser().parse(stringsFile).string.find { it.@name.equals name }.text()
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

def jks_keyAlias    = hasProperty('jks_keyAlias') ? System.getenv('jks_keyAlias') : 'testKey'
def jks_keyPassword = hasProperty('jks_keyPassword') ? System.getenv('jks_keyPassword') : '123456'
def jks_storePassword = hasProperty('jks_storePassword') ? System.getenv('jks_storePassword') : '123456'


android {

    signingConfigs {
        release {
            keyAlias jks_keyAlias
            keyPassword jks_keyPassword
            storeFile file('testKey/test.jks')
            storePassword jks_storePassword
        }
    }

    defaultConfig {

        manifestPlaceholders = [ "fabric_api_key":getApiField("fabric_api_key")]

        applicationId "com.nyrds.pixeldungeon.ml"
        versionCode 568
        versionName "remix.28.3.fix.18"
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            minifyEnabled false
        }
        debug {
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    dexOptions {
        javaMaxHeapSize "4g"
        additionalParameters = ["--set-max-idx-number=45000"]
    }
    lintOptions {
        abortOnError false
    }
    flavorDimensions "minApi"
    productFlavors {
        googlePlayRetro {
            buildToolsVersion '23.0.3'
            minSdkVersion 9
            compileSdkVersion 23
            targetSdkVersion 23
            dimension "minApi"

        }
        googlePlay {
            buildToolsVersion '27.0.3'
            targetSdkVersion 27
            minSdkVersion 14
            compileSdkVersion 27

            versionCode defaultConfig.versionCode + 10000
            // Enabling multidex support.
            multiDexEnabled true
            dimension "minApi"
        }

    }
}

apply plugin: 'io.fabric'

dependencies {
    implementation 'com.google.code.gson:gson:2.8.4'
    implementation 'org.luaj:luaj-jse:3.0.1'

    implementation 'info.guardianproject.netcipher:netcipher:1.2'


    //noinspection GradleDependency
    googlePlayRetroImplementation 'com.android.support:support-v4:23'
    //noinspection GradleDependency
    googlePlayRetroImplementation 'com.google.android.gms:play-services-ads:9.0.2'
    //noinspection GradleDependency
    googlePlayRetroImplementation 'com.google.android.gms:play-services-base:9.0.2'
    //noinspection GradleDependency
    googlePlayRetroImplementation 'com.google.android.gms:play-services-analytics:9.0.2'
    //noinspection GradleDependency
    googlePlayRetroImplementation "com.google.android.gms:play-services-games:9.0.2"


    googlePlayImplementation 'com.appbrain:appbrain-sdk:+@aar'

    googlePlayImplementation 'com.google.android.ads.consent:consent-library:1.0.6'
    googlePlayImplementation 'com.android.support:support-v4:27.1.1'
    googlePlayImplementation 'com.google.android.gms:play-services-ads:15.0.1'
    googlePlayImplementation 'com.google.android.gms:play-services-base:15.0.1'
    googlePlayImplementation 'com.google.android.gms:play-services-analytics:16.0.3'
    googlePlayImplementation "com.google.android.gms:play-services-games:15.0.1"


    googlePlayImplementation fileTree(dir: 'lib', include: '*.jar')
    googlePlayImplementation 'com.squareup.picasso:picasso:2.5.2' //for Inmobi
    googlePlayImplementation name: 'adcolony-sdk-3.3.4', ext: 'aar'

    googlePlayImplementation 'com.android.support:multidex:1.0.3'
    implementation project(':annotation')
    annotationProcessor project(':processor')

    implementation('com.crashlytics.sdk.android:crashlytics:2.9.5@aar') {
        transitive = true
    }
}


